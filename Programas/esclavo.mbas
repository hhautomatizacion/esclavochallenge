program esclavo_sec
'' portb    entrada     leer caracteres del display 4x20
'' porta.4  salida      led azul (negado, poner a cero enciende el led)
'' porta.5  salida      led ambar (negado, poner a cero enciende el led)
'' portc.0  salida      led rojo (negado, poner a cero enciende el led)
'' portc.1  salida      led verde (negado, poner a cero enciende el led)
'' portc.4  salida      control transmitir/recibir (poner a uno (junto con portc.5) para transmitir)
'' portc.5  salida      control transmitir/recibir (poner a uno (junto con portc.4) para transmitir)

dim CRC_ok as boolean
dim Esclavo as byte
dim Temp as word
dim Suma as word
dim Lect_prom as byte
dim Temp1_H as byte
dim Temp1_L as byte
dim Temp2_H as byte
dim Temp2_L as byte
dim Entrada1 as byte
dim Entrada2 as byte
dim Version_H as byte
dim Version_L as byte
dim Recivido as byte
dim Llamada as byte[4]
dim Mensaje as byte[96]
dim Longitud as byte
dim CRC as word
dim CRC_H as byte
dim CRC_L as byte
dim Iter as byte
dim bites as byte
dim Caracter as byte
dim Ant_Caracter as byte
dim InicioLCD as boolean
dim TimeOut3 as byte
dim TimeOut4 as byte
dim LcdStart1 as byte
dim LcdStart2 as byte
dim Contador as byte
dim LimpiaPerro as byte
sub procedure LimpiarLeds
    LimpiaPerro=0
    if testbit(STATUS,4) then
        porta.4=0
    else
        clrwdt
        porta.4=1
        porta.5=1
        portc.0=1
        portc.1=1
    end if

end sub
sub procedure Leer_Caracter
    TimeOut4=0
    TimeOut3=0
    do
        do
            do
                delay_us(300)
                caracter=portb
                inc(timeout4)
                if TimeOut4 > 200 then
                    TimeOut4=0
                    inc(timeout3)
                    if TimeOut3 > 5 then
                        Caracter=32
                        exit
                    end if
                end if
                if Caracter=0 then
                    Ant_Caracter=0
                end if
            loop until Caracter <> 0
            if Caracter=255 then
                Ant_Caracter=0
            end if
        loop until Caracter <> 255
    loop until Caracter <> ant_caracter
    Ant_Caracter=caracter
end sub
sub procedure Inicio_LCD
    timeout4=0
    TimeOut3=0
    InicioLCD=false
    do
        Ant_Caracter=caracter
        delay_us(300)
        Caracter=portb
        inc(timeout4)
        if TimeOut4 > 200 then
            TimeOut4=0
            inc(timeout3)
            if TimeOut3 > 30 then
                exit
            end if
        end if
    loop until (Caracter=lcdstart1) and (Ant_Caracter = LcdStart2)
    if (Caracter=lcdstart1) and (Ant_Caracter = LcdStart2) then
        InicioLCD=true
        Ant_Caracter=128
    end if
end sub
sub procedure Leer_LCD


    Inicio_LCD
    
    if InicioLCD then
        'porta.4=0 'encender led azul
        contador=7
        do
            Leer_Caracter
            select case Caracter
            case 65,66,67,68,69,70,71,72,73,74,75,76,77,78,79,80,81,82,83,84,85,86,87,88,89,90
                contador=contador+1
                Mensaje[contador]=caracter
            case 97,98,99,100,101,102,103,104,105,106,107,108,109,110,111,112,113,114,115,116,117,118,119,120,121,122
                contador=contador+1
                Mensaje[contador]=caracter
            case 48,49,50,51,52,53,54,55,56,57,58
                contador=contador+1
                Mensaje[contador]=caracter
            case 32,35,40,41,42,45,46,61,95
                contador=contador+1
                Mensaje[contador]=caracter
            end select

        loop until contador > 91
    end if
    'porta.4=1 'apagar led azul
end sub
sub procedure CalcularCRC()
    crc=65535
    for iter=0 to Longitud - 1
        crc=crc xor mensaje[iter]
        for bites= 1 to 8
            if crc mod 2 then
               crc=(crc / 2) xor 40961
            else
                crc=crc / 2
            end if
        next bites
    next iter
    CRC_h=hi(crc)
    CRC_l=lo(crc)
end sub

sub procedure interrupt()
    GIE_bit = 0
    if uart1_data_ready=1 then
        Recivido=uart1_read
        llamada[0]=llamada[1]
        llamada[1]=llamada[2]
        llamada[2]=llamada[3]
        llamada[3]=recivido
        if Llamada[1]=esclavo then
            mensaje[0]=llamada[0]
            Mensaje[1]=llamada[1]
            Longitud=2
            CalcularCRC
            CRC_ok=false
            if Llamada[3]=crc_h then
                if Llamada[2]=crc_l then
                    if Llamada[0]=15 then
                        portc.0=0                ' encender led rojo
                        portc.1=1                ' apagar led verde
                    end if
                    if Llamada[0]=6 then
                        portc.0=1                ' apagar el led rojo
                        portc.1=0                ' encender led verde
                        if InicioLCD then
                            LimpiaPerro=0
                        end if
                        porta.4=1
                    end if
                    if Llamada[0]=5 then
                        clrwdt
                        CRC_ok=true
                        porta.5=0                ' encender led ambar
                        portc.0=1                ' apagar el led rojo
                        portc.1=1                ' apagar led verde
                        Suma=0
                        for Iter=1 to lect_prom
                            adc_init
                            Temp = adc_read(0)
                            Suma=suma+temp
                        next Iter
                        Temp=suma/lect_prom
                        Temp1_H=hi(temp)
                        Temp1_L=lo(temp)
                        Suma=0
                        for Iter=1 to Lect_prom
                            adc_init
                            Temp=adc_read(1)
                            Suma=suma+temp
                        next Iter
                        Temp=suma/lect_prom
                        Temp2_H=hi(temp)
                        Temp2_L=lo(temp)
                        if portc.2=0 then
                            Entrada1=1
                        else
                            Entrada1=0
                        end if
                        if portc.3=0 then
                            Entrada2=1
                        else
                            Entrada2=0
                        end if
                        longitud=94
                        Mensaje[0]=0
                        mensaje[1]=esclavo
                        Mensaje[2]=temp1_h
                        Mensaje[3]=temp1_l
                        Mensaje[4]=temp2_h
                        Mensaje[5]=temp2_l
                        Mensaje[6]=entrada1
                        Mensaje[7]=entrada2
                        Mensaje[92]=version_h
                        Mensaje[93]=version_l
                        calcularcrc
                        portc.4=1
                        portc.5=1
                        for Iter=0 to longitud   -1
                            uart1_write(mensaje[iter])
                        next iter
                        uart1_write(crc_l)
                        uart1_write(crc_h)
                        delay_ms(3)
                        if InicioLCD=false then
                            for Iter=0 to 95
                                Mensaje[iter]=32
                            next Iter
                        end if
                        portc.4=0
                        portc.5=0
                        porta.5=1                     ' apagar led ambar
                    end if
                end if
            end if

        end if
    end if
    GIE_bit = 1                   ' enable all interrupts
end sub

main:
    option_reg=%11011111
    trisa=%00001111
    trisb=%11111111
    trisc=%10001100
    adcon1=%1101
    Version_H=1
    Version_L=27
    Esclavo = eeprom_read(0)
    LcdStart1 = eeprom_read(1)
    LcdStart2 = eeprom_read(2)
    if LcdStart1 = LcdStart2 then
        LcdStart1=255
        LcdStart2=128
    end if
    Lect_prom=4

    portc.4=0
    portc.5=0
    limpiarleds
    uart1_init(9600)
    RCIE_bit = 1                  ' enable interrupt on UART1 receive
    TXIE_bit = 0                  ' disable interrupt on UART1 transmit
    PEIE_bit = 1                  ' enable peripheral interrupts
    GIE_bit = 1                   ' enable all interrupts
    clrwdt
    while true
        Leer_LCD
        inc(LimpiaPerro)
        if LimpiaPerro < 40 then
            clrwdt
        end if
    wend
end.